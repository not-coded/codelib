plugins {
	id 'fabric-loom' version '0.13-SNAPSHOT'
	id "com.modrinth.minotaur" version "2.+"
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version + "+" + project.minecraft_version
group = project.maven_group

repositories { }

dependencies {
	//to change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.officialMojangMappings()
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// PSA: Some older mods, compiled on Loom 0.2.1, might have outdated Maven POMs.
	// You may need to force-disable transitiveness on them.
}

// Custom Manifest and Intermediary mappings declaration
loom {
	customMinecraftManifest = "https://gist.githubusercontent.com/rizecookey/4c6142baaccc3875f9b227fe22f2ace5/raw/c8ed74b19f7a5315813c9d4b199798b692a8f359/1.16_combat-6.json"
	intermediaryUrl = "https://maven.rizecookey.net/net/fabricmc/intermediary/%1\$s/intermediary-%1\$s-v2.jar"
}

processResources {
	inputs.property "version", project.version
	filteringCharset "UTF-8"

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
}

java {
	archivesBaseName = project.archives_base_name
	withJavadocJar()
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}" }
	}
}

modrinth {
	token = System.getenv("MODRINTH_TOKEN")
	projectId = "codelib"
	versionNumber = "${project.version}"
	versionName = "v${project.version}".substring(0, 5) + " | ${project.minecraft_version}"
	versionType = "release"
	uploadFile = jar
	additionalFiles.add(javadocJar)
	additionalFiles.add(sourcesJar)
	gameVersions.add("${project.minecraft_version}")
	loaders.add("fabric")
	loaders.add("quilt") // i just added it cuz :+1:
	changelog = "## Look at https://github.com/not-coded/codelib/releases/tag/${project.version}."
}